import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask
import io.franzbecker.gradle.lombok.task.DelombokTask
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    apply from: 'versions.gradle'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootGradlePluginVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufGradleVersion}")

        // some example projects use jmolecules which allows syntactic sugar via bytebuddy
        // bytebuddy plugin is disabled by default and must be manually enabled via
        // 1) `apply plugin: 'net.bytebuddy.byte-buddy-gradle-plugin'`
        // 2) specify bytebuddy plugin `byteBuddy { transformation { plugin = JMoleculesPlugin.class } }`
        // by submodules.
        classpath platform("org.jmolecules:jmolecules-bom:${jmoleculesBomVersion}")
        classpath 'org.jmolecules.integrations:jmolecules-bytebuddy'
    }
}

plugins {
    id 'idea'
    id 'nebula.release' version '19.0.10'
    id 'nebula.lint' version '20.5.5'
    id 'com.netflix.nebula.project' version '11.0.0'
    id 'com.netflix.nebula.facet' version '11.0.0'
    id 'com.github.spotbugs' version '5.2.1' apply false
    id 'net.bytebuddy.byte-buddy-gradle-plugin' version "$bytebuddyPluginVersion" apply false
    id 'org.springframework.boot' version "$springBootGradlePluginVersion" apply false
    id 'io.franzbecker.gradle-lombok' version '5.0.0' apply false
}

contacts {
    'theborakompanioni+github@gmail.com' {
        moniker 'theborakompanioni'
        github 'theborakompanioni'
    }
}

configurations.configureEach {
    resolutionStrategy {
        // check for updates in modules every build
        cacheChangingModulesFor 0, 'seconds'
        preferProjectModules()
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()
    }
}

allprojects {
    apply plugin: 'nebula.lint'

    group = 'io.github.theborakompanioni'

    gradleLint {
        alwaysRun = false // do not automatically run - must be executed manually
        autoLintAfterFailure = false // only run lint after a successful build
        rules = [
                // just warn because it has some false positives e.g. when excluding slf4j-log4j12 for moquette
                'unused-exclude-by-dep'
        ]
        criticalRules = [
                'dependency-parantheses',
                'overridden-dependency-version',
                'unused-exclude-by-conf',
                'duplicate-dependency-class',
                'dependency-tuple-expression'
        ]
    }
}

configure(rootProject) {
    defaultTasks 'clean', 'build', 'integrationTest'

    task clean {
        doLast {
            delete fileTree("${rootProject.projectDir}") {
                include '**/*.log'
                include '**/*.log.*.gz'
                include '**/*.log.*.tmp'
            }
        }
    }
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.netflix.nebula.project'
    apply plugin: 'com.netflix.nebula.info'
    apply plugin: 'com.netflix.nebula.facet'
    apply plugin: 'com.netflix.nebula.integtest-standalone'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'io.spring.dependency-management'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        toolchain {
            languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_21.getMajorVersion())
        }
    }

    repositories {
        mavenCentral()
        // needed for "spring-ai"
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.jmolecules:jmolecules-bom:${jmoleculesBomVersion}"
            mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
        }
    }

    dependencies {
        compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
        testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
        spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugsPluginVersion}"

        implementation 'org.slf4j:slf4j-api'
        implementation "com.google.guava:guava:${guavaVersion}"

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    compileJava {
        options.compilerArgs << '-parameters'
    }

    jar {
        exclude('*-local.yml')
        exclude('**/mainnet/**')
        exclude('**/*.proto')

        includeEmptyDirs false
    }

    sourcesJar {
        // needed till duplicate bug in protobuf gradle plugin is resolved.
        // see https://github.com/google/protobuf-gradle-plugin/issues/522 (last checked 2023-01-18)
        duplicatesStrategy DuplicatesStrategy.EXCLUDE

        archiveClassifier.set('sources')
        from sourceSets.main.java
    }

    tasks.register('testJar', Jar) {
        archiveClassifier.set('test')
        from sourceSets.test.output
    }

    pluginManager.withPlugin('org.springframework.boot') {

        dependencies {
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
        }

        jar {
            enabled = true
        }

        bootJar {
            // creates a launch script for the executable jar
            launchScript()
            archiveClassifier.set('boot')

            exclude('*-local.yml')
            exclude('**/*.proto')

            includeEmptyDirs false
            duplicatesStrategy DuplicatesStrategy.FAIL
        }

        bootRun {
            enabled = true
            args = ['--spring.profiles.active=development']
        }

        springBoot {
            buildInfo {
                properties {
                    // overwrite "time" property to enable reproducible builds
                    // see https://reproducible-builds.org/docs/timestamps/
                    time = System.getenv('SOURCE_DATE_EPOCH') ?: null
                }
            }
        }
    }

    facets {
        e2eTest {
            parentSourceSet = 'test'
            includeInCheckLifecycle = false
        }
    }

    test {
        finalizedBy jacocoTestReport // report is always generated after tests run

        useJUnitPlatform()
    }

    integrationTest {
        useJUnitPlatform()
    }

    e2eTest {
        useJUnitPlatform()
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report

        /*reports {
            xml.enabled = true
            html.enabled = true
            csv.enabled = false
        }*/
    }

    artifacts {
        archives testJar
    }

    lombok {
        version = "${lombokVersion}"
        sha256 = null // skip verifyLombok task - will be done by gradle dependency verification
    }

    tasks.withType(Test).configureEach {
        testLogging {
            // set options for log level LIFECYCLE
            events TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED
            showExceptions true
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showStackTraces true

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    tasks.register('dependencyTree', DependencyReportTask) {}

    tasks.register('delombok', DelombokTask) {
        dependsOn compileJava
        onlyIf { project.hasProperty('javadocEnabled') }
        def srcDirs = sourceSets.main.java.srcDirs.findAll { it.exists() }

        enabled = subprojects.isEmpty() && !srcDirs.isEmpty()

        ext.outputDir = layout.buildDirectory.dir("./delombok").get().asFile
        outputs.dir(outputDir)
        srcDirs.each {
            inputs.dir(it)
            args(it, "-d", outputDir)
        }

        doFirst {
            outputDir.deleteDir()
        }
    }

    javadoc {
        onlyIf { project.hasProperty('javadocEnabled') }
        dependsOn delombok
        source = delombok.outputDir
        failOnError = false
    }

    spotbugs {
        effort = Effort.MAX
        reportLevel = Confidence.LOW
        ignoreFailures = false
        excludeFilter = rootProject.file('./spotbugs-exclude.xml')
        reportsDir = rootProject.file("$rootProject.layout.buildDirectory/reports/spotbugs/$project.name")
    }
    tasks.withType(SpotBugsTask).configureEach {
        onlyIf { gradle.taskGraph.hasTask('build') }
        reports {
            html.required = true
            xml.required = false
        }
    }

    checkstyle {
        toolVersion = "${checkstyleVersion}"
        configFile = rootProject.file('./checkstyle.xml')
        maxErrors = 0
        ignoreFailures = false
        reportsDir = rootProject.file("$rootProject.layout.buildDirectory/reports/checkstyle/$project.name")
    }
    tasks.withType(Checkstyle).configureEach {
        onlyIf { gradle.taskGraph.hasTask('build') }
        /*reports {
            xml.enabled = false
            html.enabled = true
        }*/
    }

    tasks.withType(AbstractArchiveTask).configureEach {
        // activate reproducible archives
        // see https://docs.gradle.org/current/userguide/working_with_files.html#sec:reproducible_archives
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    infoBroker {
        // exclude properties that prevent reproducible builds
        excludedManifestProperties = [
                'Created-By',
                'Built-By',
                'Build-Date',
                'Build-Date-UTC',
                'Build-Host',
                'Built-OS',
                'Build-Timezone',
                'Build-Url',
        ]
    }
}

// disable test tasks in check phase for all '*-example-application' subprojects.
// you can invoke them manually via `./gradlew check -PexampleTest`
configure(subprojects.findAll { project -> project.name.indexOf('-example-application') > 0 }) {
    test.onlyIf { true }
    integrationTest.onlyIf { true }
}

// enable publishing for all subproject except for "example applications"
configure(subprojects.findAll { project -> project.subprojects.isEmpty() &&
        !project.pluginManager.hasPlugin('org.springframework.boot') &&
        project.name.indexOf('-example-application') < 0 }) {
    apply from: "${project.rootDir}/publish.gradle"
}
